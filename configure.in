AC_INIT(ming.i)

MAJOR_VERSION=0
MINOR_VERSION=4
MICRO_VERSION=0-beta

MING_VERSION=${MAJOR_VERSION}.${MINOR_VERSION}.${MICRO_VERSION}

dnl Tell configure to look in the config subdirectory
dnl for config.guess, config.sub, etc
AC_CONFIG_AUX_DIR(config)

dnl GNU recommended way to determine host variables (OS, etc)
AC_CANONICAL_HOST

AC_PROG_CC
AC_PROG_CXX
AC_PROG_INSTALL
AC_PATH_PROG(PYTHON, python)

dnl Check if the X libraries are installed (needed for libungif on at least Solaris)
AC_CHECK_LIB(X11, XGetImage, XLIB="-lX11", XLIB="")

dnl Check for the gif or ungif libraries
AC_CHECK_LIB(gif, PrintGifError, GIFLIB="-lgif", GIFLIB="")
dnl MinGW check for libungif
AC_CHECK_LIB(ungif, DGifOpen, GIFLIB="-lungif")
dnl Solaris needs -lX11 on the linker line for ungif to work
AC_CHECK_LIB(ungif, PrintGifError, GIFLIB="-lungif",, "-lX11")

dnl Check for the png library
dnl Solaris needs -lm on the linker line, and other platforms aren't bothered having it there. :)
AC_CHECK_LIB(png, png_read_image, PNGLIB="-lpng", PNGLIB="", "-lm")

dnl Check for the zlib library
AC_CHECK_LIB(z, compress2, ZLIB="-lz", ZLIB="")

dnl Windows has the zlib library, but its named zdll instead
dnl Note the smoke-and-mirrors setting of HAVE_LIBZ if it's found. :) (should allow MinGW to work)
AC_CHECK_LIB(zdll, compress2, ZLIB="-lzdll", ZLIB="")
AC_CHECK_LIB(zdll, compress2, AC_DEFINE([HAVE_LIBZ]), ZLIB="")

dnl Check if the vasprintf function exists on this platform
dnl (It's not present on MinGW and Solaris 10 at the time of this coding)
dnl Also check for the presence of the mkstemp function (it's not on MinGW)
AC_CHECK_FUNCS(vasprintf mkstemp)

dnl Check for various getopt functions
AC_CHECK_FUNCS(getopt getopt_long)

AC_CHECK_FUNC(sin, MATHLIB="", [
        AC_CHECK_LIB(m, sin, MATHLIB="-lm", [
		AC_ERROR([I can't find sin() function !!!])]
	)]
)

EXTRA_OBJS=
EXTRA_BINS=

if test -n "${GIFLIB}"; then
	EXTRA_OBJS="$EXTRA_OBJS gifdbl.o"
	EXTRA_BINS="$EXTRA_BINS gif2dbl gif2mask"
	AC_DEFINE(USE_GIF)
	AC_DEFINE(USE_ZLIB)
fi

if test -n "${PNGLIB}"; then
	EXTRA_OBJS="$EXTRA_OBJS pngdbl.o"
	EXTRA_BINS="$EXTRA_BINS png2dbl png2swf dbl2png"
	AC_DEFINE(USE_PNG)
	AC_DEFINE(USE_ZLIB)
fi

case "${host_os}" in
	*darwin*)
		SHLIBEXT=".\$(MING_VERSION).dylib"
		SHORT_SHLIBEXT=".\$(MAJOR_VERSION).dylib"
		NOVAR_SHLIBEXT=".dylib"
		SHLIBLDFLAGS="-dynamiclib \
			-compatibility_version \
			\`expr 1 + \"\$(MAJOR_VERSION)\"\`.\$(MINOR_VERSION) \
			-current_version \
			\`expr 1 + \"\$(MAJOR_VERSION)\"\`.\$(MINOR_VERSION) \
			-install_name \$(libdir)/\$(SHAREDLIB)"
		CFLAGS="$CFLAGS -fno-common"
		;;
	*linux*)
		SHLIBLDFLAGS="-shared -Wl,-soname,libming.so.\$(MAJOR_VERSION)"
		SHLIBEXT=".so.\$(MING_VERSION)"
		SHORT_SHLIBEXT=".so.\$(MAJOR_VERSION)"
		NOVAR_SHLIBEXT=".so"
		SHCFLAGS="-fPIC"
		;;
	*solaris*)
 		SHLIBLDFLAGS="-shared -Wl"
		SHLIBEXT=".so.\$(MING_VERSION)"
		SHORT_SHLIBEXT=".so.\$(MAJOR_VERSION)"
		NOVAR_SHLIBEXT=".so"
 		SHCFLAGS="-fPIC"
		;;
	mingw*)
		SHLIBLDFLAGS="-shared -Wl,-soname,libming.dll"
		SHLIBEXT=".dll"
		NOVAR_SHLIBEXT=".dll"
		;;
	*)
		SHLIBLDFLAGS="-shared"
		SHLIBEXT=".so.\$(MING_VERSION)"
		SHORT_SHLIBEXT=".so.\$(MAJOR_VERSION)"
		NOVAR_SHLIBEXT=".so"
esac

MACHINE=`uname -m`
if test "$MACHINE" = "x86_64"; then
	CFLAGS="$CFLAGS -fPIC"
fi

AC_SUBST(SHLIBLDFLAGS)
AC_SUBST(SHLIBEXT)
AC_SUBST(SHCFLAGS)
AC_SUBST(EXTRA_OBJS)
AC_SUBST(EXTRA_BINS)
AC_SUBST(GIFLIB)
AC_SUBST(PNGLIB)
AC_SUBST(MATHLIB)
AC_SUBST(XLIB)
AC_SUBST(ZLIB)
AC_SUBST(SHORT_SHLIBEXT)
AC_SUBST(NOVAR_SHLIBEXT)
AC_SUBST(MAJOR_VERSION)
AC_SUBST(MINOR_VERSION)
AC_SUBST(MICRO_VERSION)
AC_SUBST(MING_VERSION)

AC_CONFIG_HEADER(src/ming_config.h)

AC_OUTPUT(Makefile.config src/ming.h util/ming-config)
